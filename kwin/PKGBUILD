# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# made upon the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwin
pkgver=5.12.90
pkgrel=2.1
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kscreenlocker xcb-util-cursor plasma-framework kcmutils breeze kinit qt5-sensors)
makedepends=(extra-cmake-modules qt5-tools kdoctools)
optdepends=('qt5-virtualkeyboard: virtual keyboard support for kwin-wayland')
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-393639.patch::"https://cgit.kde.org/kwin.git/patch/?id=e3250460cc62800d706d02eda78e866efb12da55"
kdebug-393253.patch::"https://cgit.kde.org/kwin.git/patch/?id=08455f297df9e208227481cb6b0d6efbaf6fd614"
fix_message_extraction.patch::"https://cgit.kde.org/kwin.git/patch/?id=eb47c08204deca0814fd84dcf53b45e611ac4442"
kdebug-393788.patch::"https://cgit.kde.org/kwin.git/patch/?id=bed31e0557854bb92425db1eab73022c41f2c793"
kdebug-393911.patch::"https://cgit.kde.org/kwin.git/patch/?id=9a9c34f352532e2569cd537390e2e0595e88a2b4"
kdebug-392754.patch::"https://cgit.kde.org/kwin.git/patch/?id=69afe4d266ffbde364913d31fbcbc00769b4b390")
sha256sums=('3b8c283d47aa3ce86c4cac96f346e22a667cc518a23ec51923cb2e7b5dc066f3'
            'SKIP'
            '116cd6b9e15ce47a5a2a68c1474d9cf5afbb62f33a745a665096637f3e71a3c3'
            '8266e60c084a0f303d5bcfb14b3bcf84afd691523b9e04ff850f2f04ace54a04'
            'b266e1c249960e74324f132680a3698c6b97f7a78105220b1efdd3abfdc1077e'
            '8fcf3b9133da916958cad3f4f6eaf79e03a6a1e355ca1ae162eab09fd1c84034'
            '180d31e601be4667ec71eac227d0e7d98fdb038660cfba9d63fda44757f2987d'
            '8bc36d125d2c8540d40897e5769eba06606343179a2548c80f3d1fc6e67ed43c')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  #msg "Add KDEBUG 393911 patch"
  #patch -p1 -i ../kdebug-393911.patch
  
  msg "Add CCBUG 393639 patch"
  patch -p1 -i ../kdebug-393639.patch
  msg "Add KDEBUG 393253 patch"
  patch -p1 -i ../kdebug-393253.patch
  msg "Add Fix fix_message_extraction"
  patch -p1 -i ../fix_message_extraction.patch
  msg "Add KDEBUG 393788 patch"
  patch -p1 -i ../kdebug-393788.patch

  msg "Add KDEBUG 392754 patch"
  patch -p1 -i ../kdebug-392754.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
