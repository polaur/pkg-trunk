# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for PKGBUILD with upcomming fixes
# based on the original work of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwin
pkgver=5.14.4
pkgrel=2.3 #qt5.12rc2
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kscreenlocker xcb-util-cursor plasma-framework kcmutils breeze kinit qt5-sensors)
makedepends=(extra-cmake-modules qt5-tools kdoctools)
optdepends=('qt5-virtualkeyboard: virtual keyboard support for kwin-wayland')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-398100.patch::"https://cgit.kde.org/kwin.git/patch/?id=769f2659ddfff8096b826238595a26e5f21c39ad"
kdebug-264276.patch::"https://cgit.kde.org/kwin.git/patch/?id=66a91f086196f83f79dea35004aafba529b3cad9"
kdebug-400240.patch::"https://cgit.kde.org/kwin.git/patch/?id=722af0215339e6555050fd50114ac4bf2fc921fe"
kdebug-400082.patch::"https://cgit.kde.org/kwin.git/patch/?id=110fa039cbf8e7f414d8db7d4a68102d8fd9420f"
kdebug-356600.patch::"https://cgit.kde.org/kwin.git/patch/?id=fe07909cd8d24fa3fa32119508f90f18c7dac220"
kdebug-400248.patch::"https://cgit.kde.org/kwin.git/patch/?id=ce32b031f5a09cb0530a72e7602490cb05637ac0"
kdebug-375708.patch::"https://cgit.kde.org/kwin.git/patch/?id=7c3a851b040e3b7d33186ce787c43683ec076525"
kdebug-400980.patch::"https://cgit.kde.org/kwin.git/patch/?id=80da18a143f1b57751e77f9574212ff60ff14950"
kdebug-392412.patch::"https://cgit.kde.org/kwin.git/patch/?id=8af6d4f5dc74385a9d52820562162dc190f452ff"
kdebug-401499.patch::"https://cgit.kde.org/kwin.git/patch/?id=437d35eee2421565f505ebe16909349891f31cc6"
)
sha256sums=('d146dd3742400684328b5ae268b24ebfb118b2cce75ab1b8994ea883491a323d'
            'SKIP'
            '2fd27bea434e88b2a205839a49c5b69889c39db1f8ae88a118656e5bd3f6e029'
            '33930ff893eaeecf4565bd92759c6f8a53a591e9f09da375e7720345479b6215'
            'f4c110ca616741c34f05101d0a718848c4684527d95972f25524c2be08000ae7'
            '7cae63ef7808c3046479e4ef4e11f908af7f912a9421b20a58dead785465c0ec'
            'bd8357b5210a1aca35d32d805e7817efa2a2d2847db02befac75ffad5778db76'
            'd312b022bdadb8787e539d1ac29c93e93d55b9445084b64ea481df8abd9fe545'
            '9aa299eecc01603ceee14d9ffe54b25bbab3eeeecf68fc1fe65adea588a12a3b'
            '42c394bfec2f17c9239040596c7df7a96fab91ed20b7a46e05e8f0ef0763c070'
            'cb4f6b4364a21056be26f32241299c98916a264aae246dc0ceebe2cc9f1aeefd'
            '99d54a158bf66021524025adc3ef2af2564f17c4410137b3749b1ad12de12b2c')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add kdebug 398100 patch"
  patch -p1 -i ../kdebug-398100.patch
  
  msg "Add kdebug 264276 patch"
  patch -p1 -i ../kdebug-264276.patch
  
  msg "Add kdebug 400082 patch"
  patch -p1 -i ../kdebug-400082.patch
  
  msg "Add kdebug 400240 patch"
  patch -p1 -i ../kdebug-400240.patch

  msg "Add kdebug 356600 patch"
  patch -p1 -i ../kdebug-356600.patch

  msg "Add kdebug 400248 patch" # Fix 5.15.0
  patch -p1 -i ../kdebug-400248.patch

  msg "Add kdebug 375708 patch" # Fix 5.15.0
  patch -p1 -i ../kdebug-375708.patch

  msg "Add kdebug 400980 patch" #Fix 5.15.0
  patch -p1 -i ../kdebug-400980.patch

  msg "Add kdebug 392412 & 400854 patch"
  patch -p1 -i ../kdebug-392412.patch

  msg "Add kdebug 401499 patch" #Fix 5.15.0
  patch -p1 -i ../kdebug-401499.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make -j5
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
