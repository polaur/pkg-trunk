# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kscreen
pkgver=5.20.90
pkgrel=1.1
pkgdesc="KDE's screen management software"
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(libkscreen kdeclarative qt5-graphicaleffects qt5-sensors)
makedepends=(extra-cmake-modules plasma-framework kcmutils)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-432249.patch::"https://invent.kde.org/plasma/kscreen/commit/8c3823ffe77d9f1e2759461274dd478e4f7fd16d.patch")
sha256sums=('6ae4bdda3c84d570557e7649d31ef107a524732c1f637a6378420f4f7d7f9be6'
            'SKIP'
            'e81befc7c6f4c6dfe2f81514b06b2624d9266a2126d05572b3bc80a90e728be9')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 432249 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432249.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
