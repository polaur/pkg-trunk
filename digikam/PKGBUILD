# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for 7.0.0 beta version with additional patches 
# made upon a original works by:
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=digikam
_pkgver=7.0.0-beta3
pkgver=${_pkgver//-/_} # for beta versions
pkgrel=5
pkgdesc="An advanced digital photo management application"
arch=(x86_64)
license=(GPL)
url="https://www.digikam.org/"
depends=(lensfun opencv akonadi-contacts knotifyconfig libksane kfilemetadata qtav marble-common threadweaver kcalendarcore
         qt5-xmlpatterns imagemagick glu)
makedepends=(extra-cmake-modules doxygen eigen boost kdoctools libkvkontakte xorgproto graphviz)
optdepends=('hugin: panorama tool' 'qt5-imageformats: support for additional image formats (WEBP, TIFF)'
            'libkvkontakte: VKontakte plugin'
            'rawtherapee: RAW import' 'darktable: RAW import')
source=("https://download.kde.org/unstable/$pkgname/$pkgname-$_pkgver.tar.xz"{,.sig}
digikam-opencv-4.2.patch
)

sha256sums=('97b2f768674afaaca6feb6e661d5c8fc6b88b08f4d4bbab8a6bcc94c19914893'
            'SKIP'
            'fcd9a18dee3b4d009ec7256207d1b6645d2e2aa918b5cf9f9dc9b44db41f6a7b')
validpgpkeys=(D1CF2444A7858C5F2FB095B74A77747BC2386E50) # digiKam.org (digiKam project) <digikamdeveloper@gmail.com>

prepare() {
  mkdir -p build

#  patch -d $pkgname-$_pkgver -p1 -i $srcdir/digikam-opencv-4.2.patch
  }

build() {
  cd build

  cmake ../$pkgname-$_pkgver \
    -DBUILD_TESTING=OFF \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_APPSTYLES=ON \
    -DENABLE_QWEBENGINE=ON \
    -DOpenGL_GL_PREFERENCE=GLVND
  make 
  }

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
