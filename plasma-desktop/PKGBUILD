# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=plasma-desktop
pkgname=(plasma-desktop knetattach)
pkgver=5.19.90
pkgrel=3.5
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-426737.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/90a5e9317b6073fc85c3a7a2395694114847b3bd.patch"
commit-41c47566.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/41c47566f736fefe1e7589efd9d21bd3d05cbe33.patch"
kdebug-374575.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/33fd305870d2be3996fa79a2480db1a7521bca3c.patch"
kdebug-419517.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/5e74109b6ebbf785c0c27f485182de38fa21913c.patch"
kdebug-426925.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/cd57f5fb33177d090130e16dfe051d8c8481b056.patch"
commit-e7cb58f1.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/e7cb58f156a240c70c085be38a12e2a01dc5ede6.patch"
commit-e16d5d0a.patch::"https://invent.kde.org/plasma/plasma-desktop/-/commit/e16d5d0a813cede7ebd0459876f4560c3c695318.patch"
kdebug-427076.patch::"https://invent.kde.org/plasma/plasma-desktop/commit/250efd62ef5c4a121d775498b73d01e5f7b01975.patch"
)
depends=(polkit-kde-agent libxkbfile kmenuedit systemsettings ksysguard baloo libibus accountsservice)
makedepends=(extra-cmake-modules kdoctools xf86-input-evdev xf86-input-synaptics xf86-input-libinput xorg-server-devel scim kdesignerplugin kaccounts-integration intltool)
groups=(plasma)
sha256sums=('b5b78212257b8d352b0c86a321dedba65fd05d85d5395ebec4e357331ade491e'
            'SKIP'
            'cae33655f4e0c41567fbb2855ea96891f66249a4ae2d002a30de8c2ac69102da'
            'cd163ecceebcc7a5e7a95b526708c610ac5cf3dd47289537d2a797d144d8addc'
            'c080a27d32257ccff324c79aef49973077a312f99562e7a4c6a96dc3cb3ed6b1'
            '746b29070bcc127aaf0f1ff5286cdb780826a0a93818de2575ff4f4d60ae0c1b'
            '5749ad7c0b540da1b0b33900188077b05a27ae6bf39a1290988fe85f7989210c'
            '6402e1d14206ada8e00bc78ce519756e1f8d848583cd734abc40e15189df1c06'
            '7459ab8208703e0593eea7a3a6857143a7a8719c20cbb44cd82d1f09d6f4d626'
            '563c8fb8c5938167c34c3550ba336a4ac493dae4775d79230fd0f517e941669a')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 426737 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-426737.patch
    msg "Add Highlight changed settings; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../commit-41c47566.patch
    msg "Add kdebug 374575 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-374575.patch
    msg "Add kdebug 419517 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-419517.patch
    msg "Add kdebug 426925 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-426925.patch
    msg "Add force Emojier using color emoji font patch"
    patch -d $pkgname-$pkgver -p1 -i ../commit-e7cb58f1.patch
    msg "Add kcms/krunner use more accurate text for the positioning patch"
    patch -d $pkgname-$pkgver -p1 -i ../commit-e16d5d0a.patch
    msg "Add kdebug 427076 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427076.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_plasma-desktop() {
  depends+=(knetattach)
  optdepends=('plasma-nm: Network manager applet'
              'powerdevil: power management, suspend and hibernate support'
              'kscreen: screen management'
              'ibus: kimpanel IBUS support'
              'scim: kimpanel SCIM support')

  DESTDIR="$pkgdir" cmake --install build

# Split knetattach
  rm "$pkgdir"/usr/{bin/knetattach,share/applications/org.kde.knetattach.desktop}
}

package_knetattach() {
  pkgdesc='Wizard which makes it easier to integrate network resources with the Plasma Desktop'
  depends=(kdelibs4support)

  DESTDIR="$pkgdir" cmake --install build/knetattach
}
