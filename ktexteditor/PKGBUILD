# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# based on PKGBUILD wrote by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
_rcver=rc1
pkgver=5.69.0
pkgrel=0.3 # Qt5.15b3 rebuild
pkgdesc='Advanced embeddable text editor'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kparts syntax-highlighting libgit2 editorconfig-core-c)
makedepends=(git extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("git+https://anongit.kde.org/$pkgname.git#tag=v${pkgver}-$_rcver"
revert-premature-port-to-unreleased-qt-5.15.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=b70654b6bddd0b15dfa490b28284eec0dc7dba9d")
sha256sums=('SKIP'
            'b657360b6f1cdcc6e31ce25ae46f8c621e2268b0078b78b96920bbf1466b4ed0')

prepare() {
  mkdir -p build
  cd $pkgname
  patch -p1 -i ../revert-premature-port-to-unreleased-qt-5.15.patch # make it compile against Qt 5.15beta3
}

build() {
  cd build
  cmake ../$pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
