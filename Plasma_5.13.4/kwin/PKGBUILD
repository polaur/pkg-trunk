# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches of 5.13 release
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwin
pkgver=5.13.4
pkgrel=1.1
pkgdesc='An easy to use, but flexible, composited Window Manager'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kscreenlocker xcb-util-cursor plasma-framework kcmutils breeze kinit qt5-sensors)
makedepends=(extra-cmake-modules qt5-tools kdoctools qt5-virtualkeyboard)
optdepends=('qt5-virtualkeyboard: virtual keyboard support for kwin-wayland')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
fix_message_extraction.patch::"https://cgit.kde.org/kwin.git/patch/?id=eb47c08204deca0814fd84dcf53b45e611ac4442"
kdebug-391907.patch::"https://cgit.kde.org/kwin.git/patch/?id=c629d282acceaff5c6f35f06f20bdee52b167142")
sha256sums=('86b5129a06636c31cdb3bbed2d9fc33d5866acc6929400ec0edb02043b10d0c6'
            'SKIP'
            'b266e1c249960e74324f132680a3698c6b97f7a78105220b1efdd3abfdc1077e'
            '0f9030b6e2dd629d50bb489ebf202f186007f8ece0416ddbf55ac558cf1de77f')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add Fix fix_message_extraction"
  patch -p1 -i ../fix_message_extraction.patch
  
  msg "Add KDEBUG 391907 patch" # Fixed in 5.14
  patch -p1 -i ../kdebug-391907.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DKDE_INSTALL_QMLDIR=/usr/lib/qt/qml \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt/plugins \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
