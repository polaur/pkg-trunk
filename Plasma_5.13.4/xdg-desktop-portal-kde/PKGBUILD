# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=xdg-desktop-portal-kde
pkgver=5.13.4
pkgrel=1
pkgdesc='A backend implementation for xdg-desktop-portal using Qt/KF5'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(ki18n knotifications pipewire libepoxy kwayland kwidgetsaddons)
makedepends=(extra-cmake-modules)
provides=(xdg-desktop-portal-impl)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        xdg-desktop-portal-kde-pipewire-0.2.patch::"https://cgit.kde.org/xdg-desktop-portal-kde.git/patch/?id=96abec69"
        xdg-desktop-portal-kde-pipewire-0.2.2.patch::"https://cgit.kde.org/xdg-desktop-portal-kde.git/patch/?id=a96074bd")
sha256sums=('116f7528f3715c3accef424ab6e44a8504838d7df88bcab808056674ce0edb09'
            'SKIP'
            '1db01f59867d7fbd2863749010b1688974879cc1ec0a9d96c0d85b3c8fc39e15'
            '2666c73217e978301453ebe2e96ddc6f7c929a95f49e99811462c49a734bd2d9')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  cd $pkgname-$pkgver
  # Fix build with pipewire 0.2
#  patch -Np1 -i ../xdg-desktop-portal-kde-pipewire-0.2.patch
  patch -Np1 -i ../xdg-desktop-portal-kde-pipewire-0.2.2.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
