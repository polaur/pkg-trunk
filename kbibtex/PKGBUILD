# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for upcomming version build upon original work by:
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

pkgname=kbibtex
epoch=1
pkgver=0.8.90
pkgrel=1
pkgdesc="A BibTeX editor for KDE"
arch=(x86_64)
url='https://userbase.kde.org/KBibTeX'
license=(GPL)
depends=(poppler-qt5 qt5-webengine qt5-xmlpatterns qoauth ktexteditor)
optdepends=('okular: Document preview')
makedepends=(extra-cmake-modules kdoctools qca-qt5)
source=("https://download.kde.org/unstable/KBibTeX/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('134c2bac1233276dc1cfd8586d6049d4605c7b67ba7628ac845c1e476d439ca8'
            'SKIP')
validpgpkeys=('1808CB466328F4380685A3B1A264FD738D861F41') # Thomas Fischer <fischer@unix-ag.uni-kl.de>

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
