# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# based on PKGBUILD wrote by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
_rcver=rc1
pkgver=5.69.0
pkgrel=0.1
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kactivities kdeclarative kwayland kirigami2)
makedepends=(git extra-cmake-modules qt5-tools qt5-doc kdoctools doxygen)
groups=(kf5)
source=("git+https://anongit.kde.org/$pkgname.git#tag=v${pkgver}-$_rcver"
fix-qt-5.15-crash.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=0417bf61d53a61879702aa6e5bc64d1da557db27")
sha256sums=('SKIP'
            '3471b553dda8e69473de70fda31ad9982f394e8c1ac710fa9286873eb42c1ef2')

prepare() {
  mkdir -p build
  
  patch -d $pkgname -p1 -i $srcdir/fix-qt-5.15-crash.patch
}

build() {
  cd build
  cmake ../$pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
