# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=(plasma-workspace plasma-wayland-session)
pkgver=5.20.90
pkgrel=1.2
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(knotifyconfig libksysguard ktexteditor libqalculate kde-cli-tools appstream-qt phonon-qt5
         xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage milou prison kwin
         plasma-integration kdelibs4support kpeople kactivities-stats libkscreen kquickcharts kuserfeedback)
makedepends=(extra-cmake-modules kdoctools gpsd baloo networkmanager-qt kdesignerplugin kinit plasma-wayland-protocols)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgbase-$pkgver.tar.xz"{,.sig} kde.pam
kdebug-432032.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/6318f518dff906b2aee3c338330add01b5a71dff.patch"
kdebug-429862.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/f39049578967c410c6c23c5e91d33e2e3a61a196.patch"
kdebug-432029.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/55c390cc874dd0cd5496f180d13845673265db80.patch"
)
sha256sums=('0b46115919a225b000819928b4ff363ddd48b7014b497de0d689e72d83a39818'
            'SKIP'
            '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
            '54673cfaea008882d3ec67ec4fc0532e1f2e9e90f3457cbb1b7c3d6ff666d0cd'
            'dd26cc04e710fee2c8034c4ddd3a8df9fbcd26ccd550ae122575f1a9e7b37f62'
            '3cd5d5479ecadfca8322bdeeee44a56a58fe30ac83f6b328e248edabe756f87c')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 432032 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432032.patch
    msg "Add kdebug 432029 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432029.patch
    msg "Add kdebug 429862 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-429862.patch
}

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DPLASMA_SYSTEMD_BOOT=ON
  cmake --build build
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
              'qt5-virtualkeyboard: virtual keyboard support in lock screen'
              'baloo: Baloo search runner' 'discover: manage applications installation from the launcher')
  provides=(notification-daemon)
  backup=('etc/pam.d/kde')

  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace qt5-wayland kwayland-integration xorg-server-xwayland)
  groups=()

  install -Dm644 build/login-sessions/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
