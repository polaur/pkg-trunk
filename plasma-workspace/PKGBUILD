# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=(plasma-workspace plasma-wayland-session)
pkgver=5.20.90
pkgrel=1.5
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(knotifyconfig libksysguard ktexteditor libqalculate kde-cli-tools appstream-qt phonon-qt5
         xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage milou prison kwin
         plasma-integration kdelibs4support kpeople kactivities-stats libkscreen kquickcharts kuserfeedback)
makedepends=(extra-cmake-modules kdoctools gpsd baloo networkmanager-qt kdesignerplugin kinit plasma-wayland-protocols)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgbase-$pkgver.tar.xz"{,.sig} kde.pam
kdebug-432032.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/6318f518dff906b2aee3c338330add01b5a71dff.patch"
kdebug-429862.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/f39049578967c410c6c23c5e91d33e2e3a61a196.patch"
kdebug-432029.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/55c390cc874dd0cd5496f180d13845673265db80.patch"
kdebug-431977.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/51d1c4f89ace19f355bf0b7a07b9fbe7b0620e58.patch"
kdebug-431691.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/22e02fbbab3584932199adde24121ccceb66187b.patch"
kdebug-431961.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/fc86d38348ca3686d377a439c8b107ccd13b344d.patch"
kdebug-432250.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/99fdd20197cf5e501e426cb9a46182dffdd96eb1.patch"
)
sha256sums=('0b46115919a225b000819928b4ff363ddd48b7014b497de0d689e72d83a39818'
            'SKIP'
            '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
            '54673cfaea008882d3ec67ec4fc0532e1f2e9e90f3457cbb1b7c3d6ff666d0cd'
            'dd26cc04e710fee2c8034c4ddd3a8df9fbcd26ccd550ae122575f1a9e7b37f62'
            '3cd5d5479ecadfca8322bdeeee44a56a58fe30ac83f6b328e248edabe756f87c'
            '74b704899ed41c9dccdf4a4dea95448c2fdae706f0c26e8ff0934d8ac112150f'
            '841fce6201550accdf03ab47189cb6dbc9428fddb4f467634e5a8bf44d5726fd'
            'b07957cb069d8567cca808f977954d0723c695665654df3428c697d464599b5c'
            '27743a77afbe6d5e13e29f8c365800a5e8ffc7080c63b2059ef26c9a736ccfa1')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 432032 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432032.patch
    msg "Add kdebug 432029 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432029.patch
    msg "Add kdebug 429862 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-429862.patch
    msg "Add kdebug 431977 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-431977.patch
    msg "Add kdebug 431691 patch; fix in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-431691.patch
    msg "Add kdebug 431961 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-431961.patch
    msg "Add kdebug 432250 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432250.patch
}

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DPLASMA_SYSTEMD_BOOT=ON
  cmake --build build
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
              'qt5-virtualkeyboard: virtual keyboard support in lock screen'
              'baloo: Baloo search runner' 'discover: manage applications installation from the launcher')
  provides=(notification-daemon)
  backup=('etc/pam.d/kde')

  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace qt5-wayland kwayland-integration xorg-server-xwayland)
  groups=()

  install -Dm644 build/login-sessions/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
