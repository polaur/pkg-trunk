# Maintainer: pavbaranov <pavbaranov @ gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=(plasma-workspace plasma-wayland-session)
pkgver=5.19.90
pkgrel=1.5
pkgdesc='KDE Plasma Workspace'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(knotifyconfig libksysguard ktexteditor libqalculate kde-cli-tools appstream-qt phonon-qt5
         xorg-xrdb xorg-xsetroot kactivitymanagerd kholidays xorg-xmessage milou prison kwin
         plasma-integration kdelibs4support kpeople kactivities-stats libkscreen kquickcharts kuserfeedback)
makedepends=(extra-cmake-modules kdoctools gpsd baloo networkmanager-qt kdesignerplugin kinit)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgbase-$pkgver.tar.xz"{,.sig} kde.pam
#systemd-startup.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/256b6e1738d907cd2e4aaa0e89f113f096473258.patch"
kdebug-424709.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/913a66a6d933bceae82b656ce2a268ee74b73bbb.patch"
kdebug-426743.patch::"https://invent.kde.org/plasma/plasma-workspace/-/commit/27e3cfcac082c666523191a49a10912fe41e4465.patch"
kdebug-423526.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/3acf8f30249ef256c2a915a12048f55da0568492.patch"
kdebug-427106.patch::"https://invent.kde.org/plasma/plasma-workspace/commit/5cb38daedbfa5ef26a47d82d197c96230a004b0f.patch"
)
sha256sums=('8443749557af2f6288a144d3df46d3c0c271e7a9068fc46334faf7c3b6e2ab76'
            'SKIP'
            '00090291204baabe9d6857d3b1419832376dd2e279087d718b64792691e86739'
            'a34192c54da401cfa9f7df7958bb56c95d9a41bd328daa71e5ca2f4aea31fe49'
            '8caeab48915a6a8693bf9d8d71a460179eff59329208afaf2b2fdd2b1a100535'
            'e57019acf9faf166ffe5804e713ce09e074c0ac9941487d45c917b09ee4c7af8'
            '8313316f4b4633656c73f46025cf6e0491736544d4c05f6d60d9e4e4b293bbf6')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  sed -e 's|dbus-run-session ||' -i $pkgbase-$pkgver/login-sessions/plasmawayland.desktop.cmake # Don't start a second user dbus session
  
#  msg "Add systemd startup patch"
#  patch -d $pkgbase-$pkgver -p1 -i ../systemd-startup.patch
    msg "Add kdebug 424709 patch; fix in 5.20"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-424709.patch
    msg "Add kdebug 426743 patch"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-426743.patch
    msg "Add kdebug 427000 patch"
    patch -d $pkgbase-$pkgver -p1 -i ../kdebug-423526.patch
    msg "Add kdebug 427106 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-427106.patch
}

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation'
              'kdepim-addons: displaying PIM events in the calendar'
              'appmenu-gtk-module: global menu support for GTK2 and some GTK3 applications'
              'qt5-virtualkeyboard: virtual keyboard support in lock screen'
              'baloo: Baloo search runner' 'discover: manage applications installation from the launcher')
  provides=(notification-daemon)
  backup=('etc/pam.d/kde')

  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=(plasma-workspace qt5-wayland kwayland-integration xorg-server-xwayland)
  groups=()

  install -Dm644 build/login-sessions/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
