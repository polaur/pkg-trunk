# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-systemmonitor
pkgver=5.20.90
pkgrel=1.1
pkgdesc="An interface for monitoring system sensors, process information and other system resources"
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(GPL LGPL)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-432147.patch::"https://invent.kde.org/plasma/plasma-systemmonitor/commit/e70a993ef0c3b134833c859177b0472d5eb8ff59.patch")
depends=(ksysguard kirigami2)
makedepends=(extra-cmake-modules)
groups=(plasma)
sha256sums=('c34a5a64108cdb237d5e32e57608c83f364525740d2cdb1edba1f375a7aa7483'
            'SKIP'
            '4431e4e7492d2a58b1f31fbe1ddc3467bfc6369c78ad97a322f3293e870b7bc0')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 432147 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432147.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
