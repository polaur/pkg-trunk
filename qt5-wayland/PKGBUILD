# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-wayland
_qtver=5.15.0-beta4
pkgver=${_qtver/-/}
pkgrel=1.1
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Provides APIs for Wayland'
depends=('qt5-declarative' 'libxcomposite')
makedepends=('vulkan-headers')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/development_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
qtbug-83636.patch::"https://codereview.qt-project.org/gitweb?p=qt/qtwayland.git;a=patch;h=e72f7db39f58063267036a4e5d066e70a80184b2"
qtbug-82914.patch::"https://codereview.qt-project.org/gitweb?p=qt/qtwayland.git;a=patch;h=d18c29931b0bc889fff66bdbde89133544ba0529")
sha256sums=('3957166e89cb666b471bbc9d67c1f95364acb6cca9eb1fe50529df3e5834f915'
            'cb9c35f55227fca79026aaffb772080dd7805d2b7b3b4c63ee6bb49b691e141d'
            '7d2486a845481a547a17d24d3120d9855d54fca8d091617df7bbccf8bba15e73')

prepare() {
  mkdir -p build
  
  cd $_pkgfqn
  patch -p1 -i ../qtbug-83636.patch
  patch -p1 -i ../qtbug-82914.patch
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build

  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
