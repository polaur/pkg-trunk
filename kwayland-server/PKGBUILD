# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kwayland-server
pkgver=5.19.90
pkgrel=1.2
pkgdesc='Wayland server components built on KDE Frameworks'
arch=(x86_64)
url='https://kde.org/plasma-desktop'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules plasma-wayland-protocols wayland-protocols kwayland doxygen qt5-tools)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-396308.patch::"https://invent.kde.org/plasma/kwayland-server/commit/49a5c156f7795f59e2c17ae635d1cf3a9b6a59a9.patch"
kdebug-425233.patch::"https://invent.kde.org/plasma/kwayland-server/commit/2b1970754d96f92d3938f6912a757c82ce00b49e.patch")
sha256sums=('5b43afa4a44e6fd2166ab070d26876bbd4979e60df08b72e0303d898a252a6aa'
            'SKIP'
            'fdaa6d300b7ba383fb700fe33eee4975ac43672ffdf0d98baf1d137c07eba068'
            'd3e333126eeb2c891f6ed17a386cd0bdcab8d4657e2e34a41b00a00db9ad8b01')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 396308 patch; fix in 5.20"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-396308.patch
    msg "Add kdebug 425233 patch"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-425233.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
