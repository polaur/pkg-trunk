# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by"
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=korganizer
pkgver=20.03.80
pkgrel=1.1
pkgdesc="Calendar and scheduling Program"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kdepim-runtime kontactinterface incidenceeditor kuserfeedback)
makedepends=(extra-cmake-modules kdoctools qt5-tools boost)
optdepends=('kdepim-addons: additional plugins')
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-188848.patch::"https://cgit.kde.org/korganizer.git/patch/?id=1a0fb7cd1e294a73322e18795af8ab7a17b936c3")
sha256sums=('6725428bdb82f2528c1134606d73f1904ffe1bcd80d94a48f92a18738bb6fad1'
            'SKIP'
            '339aece6d3bd82869ae3266f6e7a0a5c8787d47e5b982220316b3038e0244a30')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Applying kdebug 188848 patch"
  patch -p1 -i ../kdebug-188848.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
