# 
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=korganizer
pkgver=18.03.80
pkgrel=1
pkgdesc="Calendar and scheduling Program"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(kdepim-runtime kontactinterface incidenceeditor)
makedepends=(extra-cmake-modules kdoctools python qt5-tools boost)
optdepends=('kdepim-addons: additional plugins')
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-392395.patch::"https://cgit.kde.org/korganizer.git/patch/?id=868ae0f068cd05f8e77b4f89ceaff21378790e28")
sha256sums=('fc1ee3a0fa5c7e82ab96286eb57a1f53ccd59319a5a2062db5027f6280998d26'
            'SKIP'
            '59836816172d54091df43ed58f5c9ecfb37c390b0deacb799927af2499daeb27')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 392395 patch"
  patch -p1 -i ../kdebug-392395.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
