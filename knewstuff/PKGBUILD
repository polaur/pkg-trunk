# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# based on PKGBUILD wrote by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knewstuff
_rcver=rc1
pkgver=5.69.0
pkgrel=0.1
pkgdesc='Support for downloading application assets from the network'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kio)
makedepends=(git extra-cmake-modules doxygen qt5-tools qt5-doc qt5-declarative)
optdepends=('kirigami2: QML components')
groups=(kf5)
source=("git+https://anongit.kde.org/$pkgname.git#tag=v${pkgver}-$_rcver"
kdebug-418466.patch::"https://cgit.kde.org/knewstuff.git/patch/?id=eb23f549ea9b6fc893b73c29784c67a163aa47b3")
sha256sums=('SKIP'
            'd45bc0fe02e7d6993dfb186b94fb63f6cea4a60f4801fd75e10ad2e72eacad64')

prepare() {
  mkdir -p build
  
  msg "Applying kdebug 418466 patch"
  patch -d $pkgname -p1 -i $srcdir/kdebug-418466.patch
}

build() {
  cd build
  cmake ../$pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
