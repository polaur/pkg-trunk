# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# based on PKGBUILD wrote by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knewstuff
_rcver=rc1
pkgver=5.70.0
pkgrel=0.2
pkgdesc='Support for downloading application assets from the network'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kio)
makedepends=(git extra-cmake-modules doxygen qt5-tools qt5-doc qt5-declarative)
optdepends=('kirigami2: QML components')
groups=(kf5)
source=("git+https://anongit.kde.org/$pkgname.git#tag=v${pkgver}-$_rcver"
kdebug-419959.patch::"https://cgit.kde.org/knewstuff.git/patch/?id=12642a1c0174bb4f0cd3163fee9f8e66f5959c32"
kdebug-416762.patch::"https://cgit.kde.org/knewstuff.git/patch/?id=25d391c6f36b16a9c4f685de1e6e8d1a5d9d71b4")
sha256sums=('SKIP'
            'a60b414fce2494f3da6bf34f9f5a051386ce84dba582ae3799a970f3228944b6'
            'ca70d72003bf003d8cca5d1869dbb40478c7c44b18fc0e87994d46e64ec0f7d3')

prepare() {
  mkdir -p build
  
  cd ${pkgname%-*}
  patch -p1 -i ../kdebug-419959.patch
  patch -p1 -i ../kdebug-416762.patch
}

build() {
  cd build
  cmake ../$pkgname \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
