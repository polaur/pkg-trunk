# pavbaranov: <pavbaranov at gmail dot com>
# for RC version
# made on original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwayland
pkgver=5.46rc
_tagver=v5.46.0-rc1
pkgrel=1
pkgdesc='Qt-style Client and Server library wrapper for the Wayland libraries'
arch=('x86_64')
url='https://www.kde.org'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules' 'doxygen' 'qt5-tools' 'git')
groups=('kf5')
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver")
sha256sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${_tagver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
