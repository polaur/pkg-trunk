# pavbaranov: <pavbaranov at gmail dot com>
# for RC version
# made on original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=breeze-icons
pkgver=5.53rc.0
_tagver=v5.53.0-rc1
pkgrel=2
pkgdesc='Breeze icon themes'
arch=('any')
url='https://community.kde.org/Frameworks'
license=('LGPL')
groups=('kf5')
makedepends=('extra-cmake-modules' 'qt5-base' 'git')
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver"
kdebug-399307.patch::"https://cgit.kde.org/breeze-icons.git/patch/?id=67312861ffa3af63d44d9256a2e259cbc4a5162f")
sha256sums=('SKIP'
            '968e9c87c87becbd902bd93aaadf935338e04066298330f8b0c89c2767d01a60')

prepare() {
  mkdir -p build

  cd $pkgname-$_tagver
  msg "Add kdebug 399307 patch" # Fix in 5.53
  patch -p1 -i ../kdebug-399307.patch
}

build() {
  cd build
  cmake ../${pkgname}-${_tagver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF
  make -j5
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
