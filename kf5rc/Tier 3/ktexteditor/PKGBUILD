# pavbaranov: <pavbaranov at gmail dot com>
# for RC version
# made on original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktexteditor
pkgver=5.57rc
_tagver=v5.57.0-rc1
pkgrel=2
pkgdesc='Advanced embeddable text editor'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kparts' 'syntax-highlighting' 'libgit2' 'editorconfig-core-c')
makedepends=('extra-cmake-modules' 'python' 'doxygen' 'qt5-tools' 'git')
groups=('kf5')
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver"
kdebug-382213.patch::"https://cgit.kde.org/ktexteditor.git/patch/?id=ad4cd4a7250801573a68b00e7767cbf374d049f0")
sha256sums=('SKIP'
            '9d43e57ca694f0feb27ccb90078da34a463228df4f8b690eae081df394e14a40')

prepare() {
  mkdir -p build
  
  cd $pkgname-${_tagver}
  msg "Apply KDEBUG 382213 patch; fix in 5.58"
  patch -p1 -i ../kdebug-382213.patch
}

build() {
  cd build
  cmake ../${pkgname}-${_tagver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make -j5
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
