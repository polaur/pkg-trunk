# pavbaranov: <pavbaranov at gmail dot com>
# for RC version
# made on original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcmutils
pkgver=5.52rc
_tagver=v5.52.0-rc1
pkgrel=2
pkgdesc='Utilities for interacting with KCModules'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kdeclarative')
makedepends=('extra-cmake-modules' 'python' 'doxygen' 'qt5-tools' 'git')
groups=('kf5')
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver"
kdebug-397894.patch::"https://cgit.kde.org/kcmutils.git/patch/?id=2da8aed41663d2e765dddb3bf66fad038879d6d4")
sha256sums=('SKIP'
            'bd0c7ce9fb8c3b00f64dbb8fd1ab4bcd9bb1df9853dfa27ce30d029bd9e9e3d6')

prepare() {
  mkdir -p build

  cd $pkgname-$_tagver
  msg "Add kdebug 397894 patch"
  patch -p1 -i ../kdebug-397894.patch
}

build() {
  cd build
  cmake ../${pkgname}-${_tagver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make -j5
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
