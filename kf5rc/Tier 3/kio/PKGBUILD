# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for RC version 
# made on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.53rc
_tagver=v5.53.0-rc1
pkgrel=1.1
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet desktop-file-utils)
makedepends=(extra-cmake-modules kdoctools python doxygen qt5-tools git)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries')
groups=(kf5)
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver"
D17195.diff
D17256.patch::"https://cgit.kde.org/kio.git/patch/?id=84ff71997b302aa3c957f3b023cbbc68f0d47b77"
D16949.patch::"https://cgit.kde.org/kio.git/patch/?id=d48a506d83e1527171d580d548d86a613da1f46a")
sha256sums=('SKIP'
            '5494d62a89151e138adfdbbfe6c27d184ab31de052ae203502a83912b926f408'
            'fb03d4c5bf3216bd14ae5f67a2f91b8af12ca2d7303f8fff4158bee6967228b8'
            'dbe350fd8db3734548f3591545b9d89f148b03892e07e0ed05a22e1f1619a9f0')

prepare() {
  mkdir -p build
  
  cd $pkgname-$_tagver
  msg "Add D17195 patch; fix in 5.54"
  patch -p1 -i ../D17195.diff
  msg "Add D17256 patch"
  patch -p1 -i ../D17256.patch
  msg "Add D16949 patch"
  patch -p1 -i ../D16949.patch
  }

build() {
  cd build
  cmake ../$pkgname-$_tagver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make -j5
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
