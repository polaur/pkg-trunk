# pavbaranov: <pavbaranov at gmail dot com>
# for RC version
# made on original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kded
pkgver=5.57rc
_tagver=v5.57.0-rc1
pkgrel=2
pkgdesc='Extensible deamon for providing system level services'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kinit')
makedepends=('extra-cmake-modules' 'kdoctools' 'git')
groups=('kf5')
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver"
kdebug-387556.patch::"https://cgit.kde.org/kded.git/patch/?id=496b785f2218c240326c7c98d47bede4b140f3bf")
sha256sums=('SKIP'
            '8f3241c152b0e1fc89dc1051a448850c935fd7613e63717f11077750b8c45683')

prepare() {
  mkdir -p build
  
  cd $pkgname-${_tagver}
  msg "Apply KDEBUG 387556 patch"
  patch -p1 -i ../kdebug-387556.patch
}

build() {
  cd build
  cmake ../${pkgname}-${_tagver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make -j5
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
