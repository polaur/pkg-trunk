# pavbaranov: <pavbaranov at gmail dot com>
# for RC version
# made on original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.47rc
_tagver=v5.47.0-rc1
pkgrel=2
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('qt5-quickcontrols' 'kactivities' 'kdeclarative' 'kwayland')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools' 'python' 'doxygen' 'kirigami2' 'git')
groups=('kf5')
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver"
kdebug-394423.patch::"https://cgit.kde.org/plasma-framework.git/patch/?id=d15f0fa8322dc2632f15024457bea41e7dd058d7")
sha256sums=('SKIP'
            '4653e456c2b20d642a7d6e92eca1760e9f8ac1d89ab061eb65449a367b05781a')

prepare() {
  mkdir -p build
  
  cd ${pkgname}-${_tagver}
  msg "Add KDEBUG 394423 patch"
  patch -p1 -i ../kdebug-394423.patch
}

build() {
  cd build
  cmake ../${pkgname}-${_tagver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
