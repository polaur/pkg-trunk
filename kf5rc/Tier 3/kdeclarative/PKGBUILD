# pavbaranov: <pavbaranov at gmail dot com>
# for RC version
# made on original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeclarative
pkgver=5.57rc
_tagver=v5.57.0-rc1
pkgrel=2
pkgdesc='Provides integration of QML and KDE Frameworks'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kio' 'kpackage' 'libepoxy')
makedepends=('extra-cmake-modules' 'python' 'doxygen' 'qt5-tools' 'git')
groups=('kf5')
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver"
kdebug-364766.patch::"https://cgit.kde.org/kdeclarative.git/patch/?id=c244729b67714f2c3c471815b1cdafa6ac5e8d9d")
sha256sums=('SKIP'
            '9a4e93a7fdf0714d0ed2943d2cd9a0e614383986b39bc919d22f356077c864af')

prepare() {
  mkdir -p build
  
  cd $pkgname-${_tagver}
  msg "Apply KDEBUG 364766 patch; fix in 5.58"
  patch -p1 -i ../kdebug-364766.patch
  }

build() {
  cd build
  cmake ../${pkgname}-${_tagver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make -j5
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
