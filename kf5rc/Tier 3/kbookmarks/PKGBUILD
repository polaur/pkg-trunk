# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for RC version
# made on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbookmarks
pkgver=5.45rc
_tagver=v5.45.0-rc1
pkgrel=1
pkgdesc='Support for bookmarks and the XBEL format'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kxmlgui')
makedepends=('extra-cmake-modules' 'qt5-tools' 'python' 'doxygen' 'git')
groups=('kf5')
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver")
sha256sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${_tagver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
