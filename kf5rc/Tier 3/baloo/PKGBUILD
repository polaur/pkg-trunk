# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for RC1 version
# made on the original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo
pkgver=5.44rc
_tagver=v5.44.0-rc1
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kfilemetadata' 'kidletime' 'kio' 'lmdb')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'doxygen' 'qt5-tools')
groups=('kf5')
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver")
sha256sums=('SKIP')

prepare() {
	mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$_tagver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
