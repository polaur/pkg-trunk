# pavbaranov: <pavbaranov at gmail dot com>
# for RC version
# made on original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qqc2-desktop-style
pkgver=5.52rc
_tagver=v5.52.0-rc1
pkgrel=2
pkgdesc='A style for Qt Quick Controls 2 to make it follow your desktop theme'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
depends=(kirigami2 kiconthemes)
makedepends=(extra-cmake-modules git)
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver"
kdebug-400517.patch::"https://cgit.kde.org/qqc2-desktop-style.git/patch/?id=8fa114df0ad1d53362c13e9446b43143a6dd450b")
sha256sums=('SKIP'
            '53df2421b87fe95cc91cca7cc5917e85099536da88ad58fe246a307aede6c533')

prepare() {
  mkdir -p build

  cd $pkgname-$_tagver
  msg "Add CCBUG 400517 patch"
  patch -p1 -i ../kdebug-400517.patch
}

build() {
  cd build
  cmake ../$pkgname-$_tagver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make -j5
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
