# pavbaranov: <pavbaranov at gmail dot com>
# for RC version
# made on original work by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kauth
pkgver=5.57rc
_tagver=v5.57.0-rc1
pkgrel=2
pkgdesc='Abstraction to system policy and authentication features'
arch=('x86_64')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kcoreaddons' 'polkit-qt5' 'python' 'python2')
makedepends=('extra-cmake-modules' 'qt5-tools' 'clang' 'python-pyqt5' 'python2-pyqt5' 'doxygen' 'git')
optdepends=('python-pyqt5: to use the Python 3 bindings')
groups=('kf5')
source=("$pkgname-$_tagver::git://anongit.kde.org/$pkgname.git#tag=$_tagver"
kdebug-384294.patch::"https://cgit.kde.org/kauth.git/patch/?id=0c7e12ec9cd83cc4739eda7450c073d56d4e84c2")
sha256sums=('SKIP'
            'cd09169382d07dc4fac011f58160574cc3410ee59c2b84751f77e52fff7cdd6c')

prepare() {
  mkdir -p build
  
  cd $pkgname-${_tagver}
  msg "Apply KDEBUG 384294 patch; fix in 5.58"
  patch -p1 -i ../kdebug-384294.patch
}

build() {
  cd build
  cmake ../${pkgname}-${_tagver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make -j5
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
