# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for upcomming version build upon original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kreport
pkgver=3.1.91
pkgrel=1
pkgdesc="A framework for creation and generation of reports in multiple formats"
arch=(x86_64)
url="http://www.kexi-project.org/"
license=(GPL2)
depends=('kproperty>=3.1.91' 'qt5-webkit')
makedepends=(extra-cmake-modules python qt5-tools marble-common)
optdepends=('marble-common: support for maps in reports')
source=("https://download.kde.org/unstable/$pkgname/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6a7c8c7a75f7ea3966579cbec7f9c61edaacdb61fac3ec9ee16c4ebfaf019994'
            'SKIP')
validpgpkeys=(4866BAF713B465677A4059643C7C0E201B6524DB) # Jaros≈Çaw Staniek <staniek@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
