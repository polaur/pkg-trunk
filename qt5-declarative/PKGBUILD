# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-declarative
_qtver=5.15.0-beta4
pkgver=${_qtver/-/}
pkgrel=1.1
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for QML and JavaScript languages'
depends=('qt5-base')
makedepends=('python' 'vulkan-headers')
groups=('qt' 'qt5')
conflicts=('qtchooser')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/development_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
qtbug-49646.patch::"https://codereview.qt-project.org/gitweb?p=qt/qtdeclarative.git;a=patch;h=c8bba05dc54231dbc5dc859f125d64b640e25a41"
qtbug-83169.patch::"https://codereview.qt-project.org/gitweb?p=qt/qtdeclarative.git;a=patch;h=c83ca003248a21e368ebfbf799ef9580e0f4e6ac"
qtbug-83411.patch::"https://codereview.qt-project.org/gitweb?p=qt/qtdeclarative.git;a=patch;h=7b9b44d8f45c57df1393960807497bd7286d3471"
qtbug-83667.patch::"https://codereview.qt-project.org/gitweb?p=qt/qtdeclarative.git;a=patch;h=152bca765bab4ce55d4a649896c92c3d4a4f1b30")
sha256sums=('f7cd7a3090e2df9907dc9a9c8798bf1968144575fe3dd11061bd93b40da73998'
            'ce2d816269d832c2878b25c92d1c48e029971ed51df3c159dfdb8311cf94b543'
            '7d0d30283afd2ef77dbf888b2c3f3abf12bf4afe3fdc312a23d1763dbc865fce'
            '604065eb0dfa0741c08cce20b7434d51329e1ac07fa9debba895435b6a023aa2'
            'ef525dd5827a9e04ebc1581641af24409fdbabd2ebd3b435826d1779cb8b56d8')

prepare() {
  mkdir -p build
  
  cd ${_pkgfqn}
  patch -p1 -i ../qtbug-49646.patch
  patch -p1 -i ../qtbug-83169.patch
  patch -p1 -i ../qtbug-83411.patch
  patch -p1 -i ../qtbug-83667.patch
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Symlinks for backwards compatibility
  for b in "$pkgdir"/usr/bin/*; do
    ln -s $(basename $b) "$pkgdir"/usr/bin/$(basename $b)-qt5
  done

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
