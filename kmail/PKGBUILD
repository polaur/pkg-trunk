# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kmail
pkgver=18.03.80
pkgrel=1.1
pkgdesc="KDE mail client"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(kdepim-runtime kmail-account-wizard akonadi-import-wizard mbox-importer
         pim-data-exporter pim-sieve-editor kontactinterface libktnef)
makedepends=(extra-cmake-modules kdoctools boost python)
optdepends=('kleopatra: mail encryption support' 'kdepim-addons: additional plugins')
conflicts=(ktnef)
replaces=(ktnef)
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
dont_use_new_connect_api_as_we_connect.patch::"https://cgit.kde.org/kmail.git/patch/?id=f74a769d8ca82a6a146b31f64b29faa8654418dc")
sha256sums=('8f21ce80a3bc78578fe2e156236aa7d155b091655793de6a3f03cffa21761c3b'
            'SKIP'
            '21b8478bf47439ab99b450c22d48bf933681b5cd110948e60f0c527b36d6a170')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add Don't use new connect api as we connect/disconnect it. Otherwise it duplicates patch" # See: https://cgit.kde.org/kmail.git/commit/?id=f74a769d8ca82a6a146b31f64b29faa8654418dc
  patch -p1 -i ../dont_use_new_connect_api_as_we_connect.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
