# Maintainer: pavbaranov <pavbaranov @ gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=breeze-gtk
pkgver=5.20.90
pkgrel=1.1
pkgdesc="Breeze widget theme for GTK 2 and 3"
arch=(any)
url='https://quickgit.kde.org/?p=breeze-gtk.git'
license=(LGPL)
depends=()
makedepends=(extra-cmake-modules sassc python-cairo breeze)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-431930.patch::"https://invent.kde.org/plasma/breeze-gtk/commit/15bc5576f5e1a9ee01d1a79396984b539eb8ae87.patch")
sha256sums=('c330344cda1806906c47d51c558bf5ffcb34acb9f5f61b68f024c8731f7e7092'
            'SKIP'
            '90015c49384eb78a5840fea964234d084752c2e736e0e68a9240154a65571e12')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 431930 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-431930.patch
}

build(){
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
