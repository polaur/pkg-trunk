# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version patched with bugfixes from upcomming editions
# made upon original works of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkscreen
pkgver=5.14.90
pkgrel=1.1
pkgdesc='KDE screen management software'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(qt5-x11extras libxrandr kwayland)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-403568.patch::"https://cgit.kde.org/libkscreen.git/patch/?id=08d494c401cbd60830cecd2a4c60df889d65901c")
sha256sums=('162e954f24db40bce7f687e45418bc90d05263975ebae4b7315dc55d52d34048'
            'SKIP'
            'e5d036e8379790d5ad03e8c56d567372171edd8b2191710a24d89247ae3941a5')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Apply KDEBUG 403568 patch"
  patch -p1 -i ../kdebug-403568.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make 
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
