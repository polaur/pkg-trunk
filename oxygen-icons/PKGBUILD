# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# based on PKGBUILD wrote by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=oxygen-icons
pkgname=(oxygen-icons oxygen-icons-svg)
_rcver=rc1
pkgver=5.70.0
epoch=1
pkgrel=0
pkgdesc="The Oxygen Icon Theme"
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
makedepends=(extra-cmake-modules qt5-base git)
source=("git+https://anongit.kde.org/${pkgname}5.git#tag=v${pkgver}-$_rcver")
sha256sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgbase}5-$pkgver
  make
}

package_oxygen-icons() {
  groups=(kf5)

  cd build
  make DESTDIR="$pkgdir" install
}

package_oxygen-icons-svg() {
  pkgdesc="The Oxygen Icon Theme (Scalable Vector Graphics)"

  cd ${pkgbase}5-$pkgver
  find scalable -type f ! -name '*.sh' -exec \
    install -D -m644 "{}" "$pkgdir"/usr/share/icons/oxygen/{} \;
}
