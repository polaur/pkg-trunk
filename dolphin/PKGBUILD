# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=20.03.90
pkgrel=2.1
pkgdesc="KDE File Manager"
arch=(x86_64)
url="https://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras kcmutils kparts kinit phonon-qt5)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kde-cli-tools: for editing file type options' 'ffmpegthumbs: video thumbnails' 'kdegraphics-thumbnailers: PDF and PS thumbnails'
            'konsole: terminal panel' 'purpose: share context menu')
groups=(kde-applications kdebase)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         dolphin-revert-qt5.15-port.patch::"https://cgit.kde.org/dolphin.git/patch/?id=d868568d"
         kdebug-419938.patch::"https://cgit.kde.org/dolphin.git/patch/?id=a995a80302a91595e5ce9b6798e3859da3cf6dc2"
         kdebug-406624.patch::"https://cgit.kde.org/dolphin.git/patch/?id=8de24d5802f8b1806ac26ad270e81cff1cae2111"
         kdebug-419585.patch::"https://cgit.kde.org/dolphin.git/patch/?id=35b4c6d4df1281afd621374e686f19e654ad7bae"
         kdebug-419070.patch::"https://cgit.kde.org/dolphin.git/patch/?id=9b82b08fb18724662c69ed288b1a36a215548539")
sha256sums=('444afb9d1164ea1314cf56dc6b7b65704316cefb5afcff073e9dde60edd3a3dc'
            'SKIP'
            '538682e8c2d4786680e26f593fbc2108d60d3d1e41f7360ddbec64bce79791ab'
            '6801d54d93a749b449858272778bd1e4ca63ca1b969dd140600ce6670d5ab24e'
            '996001bfae43d848480d31b01d87011e3db4fcc37b8725469cbdd2368475b596'
            'da74a881d092c67a7c6cf63f06facac8037640f139b1520efd5bea97f49b8d65'
            '63efec41a1d9c44af2dec7dfe89b15971cbb70b7d20acb31a4169cf72e07c646')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../dolphin-revert-qt5.15-port.patch
  msg "Applying kdebug 419938 patch; fix in 20.04.0"
  patch -p1 -i ../kdebug-419938.patch
  msg "Applying kdebug 406624 patch; fix in 20.08.0"
  patch -p1 -i ../kdebug-406624.patch
  msg "Applying kdebug 419585 patch"
  patch -p1 -i ../kdebug-419585.patch
  msg "Applying kdebug 419070 patch; fix in 20.04.0"
  patch -p1 -i ../kdebug-419070.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
