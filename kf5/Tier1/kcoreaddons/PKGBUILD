# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcoreaddons
pkgver=5.49.0
pkgrel=0
pkgdesc='Addons to QtCore'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools clang python-pyqt5 doxygen sip)
optdepends=('python-pyqt5: for the Python bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
sip_fix.diff)
sha256sums=('12afb12f79505614f6d5624d6c39b90b5fc39e4b1b5ddb2203622bbeb6144203'
            'SKIP'
            'be7d52b00e3f385298a8ef3a101d6a3b6aedb9f6aaefc494dd260d9db77ef81a')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

	cd $pkgname-$pkgver
	msg "add patch"
	patch -p1 -i ../sip_fix.diff
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -D_KDE4_DEFAULT_HOME_POSTFIX=4 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
