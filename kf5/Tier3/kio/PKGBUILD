# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.49.0
pkgrel=0.1
pkgdesc='Resource and network access abstraction'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(solid kjobwidgets kbookmarks libxslt kwallet desktop-file-utils)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools)
optdepends=('kio-extras: extra protocols support (sftp, fish and more)' 'kdoctools: for the help kioslave'
            'knetattach: to add new kio-remote entries')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-392913.patch::"https://cgit.kde.org/kio.git/patch/?id=f6df95e595ee3a5322fb70c7dd5860630f82ffdd"
D14666.diff
D14610.diff
D14689.diff)
sha256sums=('f3089c1746dcd86d100b57a011e6f6fcbdf0622bad6f546e63048815de783a67'
            'SKIP'
            '775fba39e22d63022b02ec07ce3d676b3ebe4eed2ddbf64f2823bcf87325b743'
            'b58f084c10c7d9ca249561e881fd0df4122f1901bf5012f20d69e579fff80565'
            'eacda7135a860c1fe9ed8b85f53ace1a67afc54772a0a1f9a2ca449b5a593717'
            'd8633e9ceb42fa1bfb11dff5cc36d2f494cc3d3d4a4d678b4b3c20749848e17b')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add KDEBUG 392913 patch"
  patch -p1 -i ../kdebug-392913.patch
  msg "Fixes memory leak in KUrlComboBox::set Url aka D14666"
  patch -p1 -i ../D14666.diff
  msg "Use KLineEdit for folder name if folder has write access, else use QLabel aka D14610"
  patch -p1 -i ../D14610.diff
  msg "[KFileItem] Don't read directory comment on slow mounts aka D14689"
  patch -p1 -i ../D14689.diff
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
