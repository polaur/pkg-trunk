# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcmutils
pkgver=5.49.0
pkgrel=0.1
pkgdesc='Utilities for interacting with KCModules'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kdeclarative)
makedepends=(extra-cmake-modules doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-389585.patch::"https://cgit.kde.org/kcmutils.git/patch/?id=23ba2b37ef825f0efccc169455f45b0dcdac97cf")
sha256sums=('388587f864352e98670c41a9fb615cc6580c42b2b026041a341202976e4a6877'
            'SKIP'
            'c882a74caf4e2dbb6f0b06402dd98e1dbb63e1d9f15bb4969bbdb35afd68a619')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "Add KDEBUG 389585 patch"
  patch -p1 -i ../kdebug-389585.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
