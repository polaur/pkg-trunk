# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=polkit-kde-agent
pkgver=5.20.90
pkgrel=1.1
pkgdesc='Daemon providing a polkit authentication UI for KDE'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(kiconthemes kdbusaddons kcrash)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-1-$pkgver.tar.xz"{,.sig}
kdebug-431963.patch::"https://invent.kde.org/plasma/polkit-kde-agent-1/commit/102bd557798c753688f410b24449f4d28cd0f8b3.patch")
sha256sums=('2d7900984bc949cd5afe1a34447fe89ca53e26d93720eed29ec858f546f08f60'
            'SKIP'
            '2b0b3b5caf1d1feebf2299a6391612076bcd6beb10ab9589ba0c23bdcd9c7100')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 431963 patch; fix in 5.21"
    patch -d $pkgname-1-$pkgver -p1 -i ../kdebug-431963.patch
}

build() {
  cmake -B build -S $pkgname-1-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
