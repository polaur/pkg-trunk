# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# based on PKGBUILD wrote by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcrash
_rcver=rc1
pkgver=5.70.0
pkgrel=0
pkgdesc='Support for application crash analysis and bug report from apps'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kcoreaddons kwindowsystem)
makedepends=(git extra-cmake-modules doxygen qt5-tools qt5-doc)
optdepends=('drkonqi: KDE crash handler application')
groups=(kf5)
source=("git+https://anongit.kde.org/$pkgname.git#tag=v${pkgver}-$_rcver")
sha256sums=('SKIP')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
