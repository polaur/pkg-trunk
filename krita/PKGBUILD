# Maintainer; pavbaranov <pavbaranov at gmail dot com>
# for krita 4.2.0-alpha 
# made upon a original work of:
# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
pkgver=4.2.0.alpha
pkgrel=1
pkgdesc="Edit and paint images"
arch=(x86_64)
url="https://krita.org"
license=(GPL3)
depends=(kitemviews kitemmodels ki18n kcompletion karchive kguiaddons kcrash qt5-svg qt5-multimedia
         gsl libraw exiv2 openexr fftw curl boost-libs giflib hicolor-icon-theme desktop-file-utils)
makedepends=(extra-cmake-modules kdoctools boost eigen vc poppler-qt5 opencolorio python-pyqt5 libheif sip python-sip)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations' 'opencolorio: for the LUT docker'
            "krita-plugin-gmic: G'MIC plugin" 'python-pyqt5: for the Python plugins' 'libheif: HEIF filter')
conflicts=(calligra-krita krita-l10n)
replaces=(calligra-krita krita-l10n)
source=("https://download.kde.org/unstable/krita/${pkgver%.*}-alpha/$pkgname-${pkgver%.*}-alpha.tar.gz"{,.sig})
#        krita-qtabletevent.patch)
sha256sums=('7d2adade31455b95104ca63003ad417b86939030de86950e960657a343458056'
            'SKIP')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD') # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build

 # cd $pkgname-$pkgver
 # patch -p1 -i ../krita-qtabletevent.patch # Workaround tablet issues with Qt 5.12 https://cgit.kde.org/krita.git/commit/?id=a3193fe2
}

build() {
  cd build
  cmake ../$pkgname-${pkgver%.*}-alpha \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make 
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
