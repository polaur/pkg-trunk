# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-tools
_qtver=5.15.0-beta4
pkgver=${_qtver/-/}
pkgrel=1.1
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='A cross-platform application and UI framework (Development Tools, QtHelp)'
depends=('qt5-base' 'hicolor-icon-theme')
makedepends=('qt5-declarative' 'vulkan-headers' 'clang' 'qt5-webkit')
optdepends=('clang: for qdoc' 'qt5-webkit: for Qt Assistant')
groups=('qt' 'qt5')
conflicts=('qtchooser')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/development_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        {assistant,designer,linguist,qdbusviewer}.desktop
        qtbug-83913.patch::"https://codereview.qt-project.org/gitweb?p=qt/qttools.git;a=patch;h=dc5fed5555d1c904753e83fd90aeecae547146ce")
sha256sums=('e9c8b166b82b153d6777b866016a6deeee50d5b34791e98e1e2d37146a6dda46'
            '8092d6d846e39632107b14726fa112862d800e929ef10f05f2a284dbda9b53c4'
            '92f3e5dc8bc416e02893c38a05287238853e0c20732df1e62d6a672744a34e7c'
            '15781519115e36d2c67c12ee6bc636425cd445072f43cb8bdd1364cfacbe4c45'
            'cd94367e35f99ab85277aa95985bbde292a9cc82db2c04c4efdaed5799aa16db'
            'c713710af5e6c2a46db5ef139672179ae47c9cbe5f3c512b97a2448fe930cd53')

prepare() {
  mkdir -p build
  
  cd ${_pkgfqn}
  patch -p1 -i ../qtbug-83913.patch
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="${pkgdir}" install

  cd ../${_pkgfqn}
  # install missing icons and desktop files
  for icon in src/linguist/linguist/images/icons/linguist-*-32.png ; do
    size=$(echo $(basename ${icon}) | cut -d- -f2)
    install -p -D -m644 ${icon} \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/linguist.png"
  done

  install -D -m644 src/assistant/assistant/images/assistant.png \
    "${pkgdir}/usr/share/icons/hicolor/32x32/apps/assistant.png"
  install -D -m644 src/assistant/assistant/images/assistant-128.png \
    "${pkgdir}/usr/share/icons/hicolor/128x128/apps/assistant.png"
  install -D -m644 src/designer/src/designer/images/designer.png \
    "${pkgdir}/usr/share/icons/hicolor/128x128/apps/QtProject-designer.png"
  install -D -m644 src/qdbus/qdbusviewer/images/qdbusviewer.png \
    "${pkgdir}/usr/share/icons/hicolor/32x32/apps/qdbusviewer.png"
  install -D -m644 src/qdbus/qdbusviewer/images/qdbusviewer-128.png \
    "${pkgdir}/usr/share/icons/hicolor/128x128/apps/qdbusviewer.png"
  install -d "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}"/{linguist,designer,assistant,qdbusviewer}.desktop \
    "${pkgdir}/usr/share/applications/"

  # Symlinks for backwards compatibility
  for b in "${pkgdir}"/usr/bin/*; do
    ln -s $(basename $b) "${pkgdir}"/usr/bin/$(basename $b)-qt5
  done

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "${pkgdir}/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
