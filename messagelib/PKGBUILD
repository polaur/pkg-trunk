# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=20.03.80
pkgrel=1.1
pkgdesc="KDE PIM messaging library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kdepim-apps-libs kmailtransport libgravatar kidentitymanagement kmbox qca)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-416306.patch::"https://cgit.kde.org/messagelib.git/patch/?id=1c74cf3f81572c98ca599173e5170f4fda0dcf33")
sha256sums=('f9307fa33e587a8b75bc269dfb71837bea09ae27de2182112803fff4ebc08f7f'
            'SKIP'
            'b46d49b7a7dab34a2c1349e223a27ff79849d1fda97f21b6bc611c609a817595')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "KDEBUG 416306 patch; fix in 20.04.0"
  patch -p1 -i ../kdebug-416306.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
