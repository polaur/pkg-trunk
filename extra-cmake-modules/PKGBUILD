# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# based on PKGBUILD wrote by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=extra-cmake-modules
_rcver=rc1
pkgver=5.68.0.$_rcver
pkgrel=1
pkgdesc='Extra modules and scripts for CMake'
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(cmake)
makedepends=(python-sphinx python-requests qt5-tools git)
groups=(kf5)
source=("git+https://anongit.kde.org/$pkgname.git#tag=v${pkgver%.*}-$_rcver"
        ECM-no-init.py.patch)
sha256sums=('SKIP'
            '5695e45c7621a00c0bca28f058c13b5d524f963a00b53337c8cefcdaf22c4b52')

prepare() {
  mkdir -p build

  cd $pkgname
  patch -p1 -i ../ECM-no-init.py.patch # Don't create __init__.py
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_HTML_DOCS=OFF \
    -DBUILD_QTHELP_DOCS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
