# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=20.03.80
pkgrel=1.1
pkgdesc='Extends the functionality of kdepim'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL LGPL FDL)
depends=(libkolabxml kdav akonadi-calendar knotifyconfig kalarmcal kmbox pimcommon akonadi-notes akonadi qca qt5-networkauth)
makedepends=(extra-cmake-modules kdoctools boost)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-413078.patch::"https://cgit.kde.org/kdepim-runtime.git/patch/?id=4484df3d6f0c449ec2cfd4956175eab507f4cc55"
         kdepim-runtime-qt5.15.patch::"https://cgit.kde.org/kdepim-runtime.git/patch?id=d547b86f")
sha256sums=('76bcd8204b9b3c58842169b7bc3f95e141f91a500a13591955663746f04e5b94'
            'SKIP'
            '899a7d146324992b08b9cee684586a4cc2625095ce934f4dbe03d15c6cd47764'
            '32e89ed4325bfb02d192a38b2aacf4ca947f6b7ddd72f4d49e46564667ff82d5')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -Rp1 -i ../kdepim-runtime-qt5.15.patch # Fix build with Qt 5.15
  msg "Apply kdebug 413078 and 416663 patch"
  patch -p1 -i ../kdebug-413078.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
