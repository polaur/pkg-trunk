# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version patched with bugfixes from upcomming editions
# made upon original works of:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons
pkgver=5.14.90
pkgrel=1.1
pkgdesc='All kind of addons to improve your Plasma experience'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-workspace)
makedepends=(extra-cmake-modules kross qt5-webengine)
optdepends=('kross: comic applet'
            'purpose: Quickshare applet'
            'quota-tools: disk quota applet'
            'qt5-webengine: dictionary and webbrowser applets')
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
ccbug-370185.patch::"https://cgit.kde.org/kdeplasma-addons.git/patch/?id=80bd32b12a01e29f3fe88f2a50c0a2a842872c19")
sha256sums=('0c4eb88b39ecc65fc00b59f3e3724de81a8dc18cea188372427bcaf39543c8e5'
            'SKIP'
            'fab60993eeddf9e14539a62fb967e27c8628dffcc42a7ce204b170688c46eedb')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply CCBUG 370185 patch"
  patch -p1 -i ../ccbug-370185.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
	-DKDE_INSTALL_SYSCONFDIR=/etc \
	-DKDE_INSTALL_QMLDIR=/usr/lib/qt/qml \
	-DKDE_INSTALL_PLUGINDIR=/usr/lib/qt/plugins \
	-DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
