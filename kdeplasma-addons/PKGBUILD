# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with patches from upcoming releases
# made upon the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons
pkgver=5.12.90
pkgrel=1.1
pkgdesc='All kind of addons to improve your Plasma experience'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-workspace)
makedepends=(extra-cmake-modules kdoctools kross kdesignerplugin qt5-webengine)
optdepends=('kross: comic applet'
            'purpose: Quickshare applet'
            'quota-tools: disk quota applet'
            'qt5-webengine: dictionary applet')
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
fix-always-empty-memory-bar.patch::"https://cgit.kde.org/kdeplasma-addons.git/patch/?id=f54bcde16302e18371f888557c8075fdbf1bfa2c")
sha256sums=('449731d2808b4c2e839ec39325d955eab14d6753ae5c2c8723574b5c8d8058d3'
            'SKIP'
            '774fb42b881356501b9e00c1e36d88f4d035f52f0727b10406c50932d1af4a70')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Add Fix always empty memory bar patch"
  patch -p1 -i ../fix-always-empty-memory-bar.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
