# Maintainer: Daniel Karasiński <daniel dot karasinski at gmail dot com>
# Contributor: pavbaranov <pavbaranov at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>


pkgname=dooble-git
pkgver=2020.10.10.r14.g5ced817d
pkgrel=1
pkgdesc="Web browser based on QtWebEngine"
arch=(x86_64)
url="https://textbrowser.github.io/dooble/"
license=('BSD')
depends=('qt5-webengine')
source=("git+https://github.com/textbrowser/dooble.git"
	dooble_pl_PL.ts
	dooble_pl_PL.qm)
sha256sums=('SKIP'
            'd33fbf69206e790c59b3ad8d473672a9e4bef91b0c60fe803a412a698480e2b0'
            '23aaa034760bd2588fa5cbfed9d316956c7cc77ac7a0a4eef0c1e3af64e68b48')

pkgver() {
	cd "${pkgname%-*}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "${pkgname%-*}/2.x"
  sed -i 's/-Werror //' dooble.pro
  sed -i 's|Categories=Web|Categories=Network;Qt;WebBrowser;|
          s|Exec=.*|Exec=dooble|
          s|Icon=.*|Icon=dooble|' dooble.desktop
  sed -i 's|QString path(QDir::currentPath());|QString path("/usr/share/dooble");|' Source/dooble_settings.cc
  cp "${srcdir}"/dooble_pl_PL.* Translations/
}

build() {
	cd "${pkgname%-*}/2.x"
  qmake dooble.pro
  make
}

package() {
	cd "${pkgname%-*}/2.x"
  install -Dm755 Dooble "$pkgdir/usr/bin/dooble"
  install -Dm644 Icons/Logo/dooble.png "$pkgdir/usr/share/pixmaps/dooble.png"
  install -Dm644 dooble.desktop "$pkgdir/usr/share/applications/dooble.desktop"
  install -Dm644 -t "$pkgdir/usr/share/dooble/Translations" Translations/dooble_*.qm
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/dooble/LICENSE"
}
