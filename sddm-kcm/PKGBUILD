# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=sddm-kcm
pkgver=5.20.90
pkgrel=1.2
pkgdesc='KDE Config Module for SDDM'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(GPL)
depends=(sddm knewstuff)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-432112.patch::"https://invent.kde.org/plasma/sddm-kcm/commit/faef7d3c29a3e0eaab493ec55bea74a4cee98c58.patch"
kdebug-432018.patch::"https://invent.kde.org/plasma/sddm-kcm/commit/1fbb1d7937bfb9cbdd3d693155f4eb37eef94098.patch")
sha256sums=('b9478de46840e4d588859c29e02172729a03f682ee7523d75ed522ced5349511'
            'SKIP'
            '2130293ff3e68d2a2f0e648c309b5713c55a0b2993d37b9788aeaa66d6f675a5'
            '79b5a8407eb1353ccb3cf4752d592ab1d3b8b83ae651499bb773f52776625d56')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 432112 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432112.patch
    msg "Add kdebug 432018 patch; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-432018.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

