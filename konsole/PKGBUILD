# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=konsole
pkgver=20.03.80
pkgrel=1.1
arch=(x86_64)
url='https://kde.org/applications/system/konsole/'
pkgdesc="KDE's terminal emulator"
license=(GPL LGPL FDL)
groups=(kde-applications kdebase)
depends=(knotifyconfig kpty kparts kinit knewstuff)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('keditbookmarks: to manage bookmarks')
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         konsole-qt5.15.patch::"https://cgit.kde.org/konsole.git/patch/?id=5ab73956"
         kdebug-416376.patch::"https://cgit.kde.org/konsole.git/patch/?id=26f4a2218f52f790d4de087216098619a753164e")
sha256sums=('0158b3b816abe7201a92d92563fe74130490aa5959ce242bf5f618f63aed4859'
            'SKIP'
            'ecd91740432f3d0caff87bc1dbbb9e7ca855d97dcd2267558012d4583ad826a6'
            '4fb50295cf48cc7ad4178f5e092e286949eef532a90eb26c3e0738ba0fa4a9bf')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../konsole-qt5.15.patch # Fix build with Qt 5.15
  msg "Applying kdebug 416376 patch"
  patch -p1 -i ../kdebug-416376.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
