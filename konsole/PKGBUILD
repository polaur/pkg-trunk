# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for version with bugfix from github
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=konsole
pkgver=18.04.0
pkgrel=1.1
arch=(x86_64)
url='https://kde.org/applications/system/konsole/'
pkgdesc="KDE's terminal emulator"
license=(GPL LGPL FDL)
groups=(kde-applications kdebase)
depends=(knotifyconfig kpty kparts kinit)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('keditbookmarks: to manage bookmarks')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-361547.patch::"https://cgit.kde.org/konsole.git/patch/?id=a565bc97"
# kdebug-170582.patch::"https://cgit.kde.org/konsole.git/patch/?id=6b1d799a"
kdebug-392938.patch::"https://cgit.kde.org/konsole.git/patch/?id=8254dd07"
)
sha256sums=('53f829a574929c8973383257b7638ba4dc596fd2f16778b0f7f3baa0e4a04a64'
            'SKIP'
            '0d8d840cea5b5e40c92f3e3181f4b403cfd3d684c8f598fd1908309492e228a7'
            '6455b1148d076f29ac40492f26ecb01bfcfbd87082c379d7eac08302aa81f04d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  msg "kdebug 361547 patch" # See: https://bugs.kde.org/show_bug.cgi?id=361547 
  patch -p1 -i ../kdebug-361547.patch
  msg "kdebug 392938 patch"
  patch -p1 -i ../kdebug-392938.patch
#    msg "kdebug-170582.patch" # Fixed in 18.08 nie nakłada się
#  patch -p1 -i ../kdebug-170582.patch

}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
