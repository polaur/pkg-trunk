# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# for upcomming version build upon original work by:
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kproperty
pkgver=3.1.91
pkgrel=1
pkgdesc="A property editing framework with editor widget similar to what is known from Qt Designer"
arch=(x86_64)
url="http://www.kexi-project.org/"
license=(GPL2)
depends=(kwidgetsaddons kguiaddons kcoreaddons kconfig)
makedepends=(extra-cmake-modules python qt5-tools doxygen)
source=("https://download.kde.org/unstable/$pkgname/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('df2f55eaeec86cc36e9c56e66a91f1d49d4ef200467e52b1ba02a37154c28a8f'
            'SKIP')
validpgpkeys=(4866BAF713B465677A4059643C7C0E201B6524DB) # Jaros≈Çaw Staniek <staniek@kde.org>


prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
