# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=20.07.90
pkgrel=1.1
pkgdesc="KDE screenshot capture utility"
arch=(x86_64)
url='https://kde.org/applications/utilities/org.kde.spectacle'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose knewstuff kwayland qt5-tools)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kipi-plugins: export to various online services')
groups=(kde-applications kde-graphics)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-424380.patch::"https://invent.kde.org/graphics/spectacle/commit/7f88fcb4bb4085f39b7330212efacb19c2b5bcad.patch"
kdebug-422052.patch::"https://invent.kde.org/graphics/spectacle/-/commit/526b1e907372a04c147846aa509aa378d9f84218.patch"
)
sha256sums=('98fe6289faa94e66a50b5e86407d0d73bcc253205e49209b3d480b8d9a435485'
            'SKIP'
            '81c3f39c5c70acf7289e4bba739444c57096e06f1a691c7db7a56483e436e601'
            '1581f40db86f696950669b7c6aecd1d484edbdab207eb13685c81865ab4b1574')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
    msg "Add kdebug 424380 patch; fix in 20.12.0"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-424380.patch
    msg "Add kdebug 422052 patch; fix in 20.12.0"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-422052.patch
}
    
build() {
  cmake -B build -S $pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

