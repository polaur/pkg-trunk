# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=20.03.80
pkgrel=1.1
pkgdesc="KDE screenshot capture utility"
arch=(x86_64)
url='https://kde.org/applications/utilities/org.kde.spectacle'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose knewstuff kwayland qt5-tools)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('kipi-plugins: export to various online services')
groups=(kde-applications kdegraphics)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
add-missing-includes.patch::"https://cgit.kde.org/spectacle.git/patch/?id=773d569d7c973d37fe29125ed32904ca70ce7749"
kdebug-419072.patch::"https://cgit.kde.org/spectacle.git/patch/?id=6c0aa6ec8ac8065747cafe814002cec05aa34903")
sha256sums=('d35f950ad87ff9920d2677ee8af47c462c4155a589fad3283d42bcf73dcb7f6f'
            'SKIP'
            '2cf63536b7e2d24dbf019c41a5f22fa44ae70386316ea9f190649c43ee9871ce'
            '0a9dd585401c995faa7dc510d0fc70ab9216c52ac01cfd1aa59e7327fbb96ba7')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  patch -p1 -i ../add-missing-includes.patch
  msg "Applying kdebug 419072 patch"
  patch -p1 -i ../kdebug-419072.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

