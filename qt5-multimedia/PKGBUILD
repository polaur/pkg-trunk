# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-multimedia
_qtver=5.15.0-beta4
pkgver=${_qtver/-/}
pkgrel=1.1
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for audio, video, radio and camera functionality'
depends=('qt5-base' 'gst-plugins-base' 'libpulse' 'openal')
makedepends=('qt5-declarative' 'gst-plugins-bad')
optdepends=('qt5-declarative: QML bindings' 'gst-plugins-good: camera support, additional plugins'
            'gst-plugins-bad: camera support, additional plugins' 'gst-plugins-ugly: additional plugins'
            'gst-libav: ffmpeg plugin')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/development_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
qtbug-82448.patch::"https://codereview.qt-project.org/gitweb?p=qt/qtmultimedia.git;a=patch;h=19f6faf671438dc9c5165733a1b060fe2dfe1956"
qtbug-82572.patch::"https://codereview.qt-project.org/gitweb?p=qt/qtmultimedia.git;a=patch;h=b8525df9434d34f24c488848e320d1bc77de49cd"
qtbug-82582.patch::"https://codereview.qt-project.org/gitweb?p=qt/qtmultimedia.git;a=patch;h=e27ba234274a2a3325241db58a3da908952c603a"
qtbug-83417.patch::"https://codereview.qt-project.org/gitweb?p=qt/qtmultimedia.git;a=patch;h=d8d072417b08dd75734b3f0aa86c3a49db934770")
sha256sums=('9c1d0b2f11281f34f7499c44c06054e066c6dffb83948e02a3373dfccd442cd5'
            '7466656554d3909e85693f661993bd2ec7aed2258b7e72c82e29f3fe1a8c3823'
            'a2ec5f1a3d21f364bb51f45d1a9405b9c3359724d49a63c276f5d7f6fe45b2cc'
            '7f0f8712a2c38a34e4f7aac81a4a72e19c491ebdcce1ba95dcd671adb7d9d541'
            '6f08c149102a2462b5ff2b213c81775bf7f8b34c451d532e34b76e17b02eed11')

prepare() {
  mkdir -p build
  
  cd $_pkgfqn
  patch -p1 -i ../qtbug-82448.patch
  patch -p1 -i ../qtbug-82572.patch
#  patch -p1 -i ../qtbug-82582.patch
  patch -p1 -i ../qtbug-83417.patch
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
