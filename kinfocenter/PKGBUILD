# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes or improvement from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kinfocenter
pkgver=5.19.90
pkgrel=1.1
pkgdesc='A utility that provides information about a computer system'
arch=(x86_64)
url='https://www.kde.org/applications/system/kinfocenter/'
license=(LGPL)
depends=(systemsettings glu)
makedepends=(extra-cmake-modules plasma-framework kdoctools)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
commit-c50516af.patch::"https://invent.kde.org/plasma/kinfocenter/-/commit/c50516afc282e34c9f153b3c4cb8f4a9a7451ab1.patch")
sha256sums=('748bc4b3a79310a010c8396813d1f1b51f49f26ff2fd08a25433ef1d38d5419c'
            'SKIP'
            '37efd5025a7e8eff1b5867246c6b73873f4e68ab238a47e5161cf614cd94d4a0')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
              
prepare() {
    msg "Add new network interfaces page; fix in 5.21"
    patch -d $pkgname-$pkgver -p1 -i ../commit-c50516af.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
