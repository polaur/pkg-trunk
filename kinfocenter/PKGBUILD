# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# only for version with bugfixes from upcomming releases
# based on the original works by:
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kinfocenter
pkgver=5.20.90
pkgrel=1.1
pkgdesc='A utility that provides information about a computer system'
arch=(x86_64)
url='https://www.kde.org/applications/system/kinfocenter/'
license=(LGPL)
depends=(systemsettings glu)
makedepends=(extra-cmake-modules plasma-framework kdoctools)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-428382.patch::"https://invent.kde.org/plasma/kinfocenter/commit/6a5b71128bce2cc0132725ab0e06f4e07a1f8a46.patch")
sha256sums=('252cf13c3924231b2bbb344c6fdb8afb614b734f0c2422e2ace501a3a24299cc'
            'SKIP'
            'd0b5f4df8db313567cfd899277a6f2d2a400f88cd93cc4f003a4b22878fef434')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
    msg "Add kdebug 428382 patch; fix in 5.22"
    patch -d $pkgname-$pkgver -p1 -i ../kdebug-428382.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
