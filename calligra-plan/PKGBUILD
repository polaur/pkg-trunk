# Maintainer: pavbaranov <pavbaranov at gmail dot com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=calligra-plan
pkgdesc="A project management application intended for managing moderately large projects with multiple resources (beta version)"
pkgver=3.2.90
pkgrel=1
arch=(x86_64)
url='https://www.calligra-suite.org/'
license=(FDL1.2 GPL2 LGPL)
depends=(kinit akonadi-contacts kdiagram kactivities kholidays kcalendarcore khtml qca)
makedepends=(extra-cmake-modules kdoctools boost)
conflicts=('calligra<3.1.0')
source=("https://download.kde.org/unstable/calligra/calligraplan-$pkgver.tar.xz"{,.sig})
sha256sums=('3a809875dd703a440e9cb38f8326f0d9a80a603fc72a8de9ae2e9a4ad10033eb'
            'SKIP')
validpgpkeys=(05D00A8B73A686789E0A156858B9596C722EA3BD  # Boudewijn Rempt <foundation@krita.org>
              42A60D06F449E895F40F09C01638B8A616108B8A) # Dag Andersen <danders@get2net.dk>

build() {
  cmake -B build -S calligraplan-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
