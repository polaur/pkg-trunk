# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcalutils
pkgver=20.03.80
pkgrel=1.1
pkgdesc="The KDE calendar utility library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kcalendarcore kidentitymanagement)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
kdebug-410758.patch::"https://cgit.kde.org/kcalutils.git/patch/?id=1e240368ed7cd0b015761ef214e5caf504e8f723")
sha256sums=('28a6261b1737f58ccc7f9c48ecbab2e58aa20f790a7b7d51c57890cc46496a45'
            'SKIP'
            '34454d3746b049130f6710def3edc6b36ac2b43a7ca203e7a582212b845915f7')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
  
  cd $pkgname-$pkgver
  msg "Apply kdebug 410758 patch"
  patch -p1 -i ../kdebug-410758.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
